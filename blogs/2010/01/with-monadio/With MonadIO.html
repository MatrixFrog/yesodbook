<p>I've been bitten by this one before: I'd like to use a monad transformer, and I'd like to use a "with" function. In this case, we'll give the example of <a href="http://www.haskell.org/ghc/docs/latest/html/libraries/base/Foreign-C-String.html#v%3AwithCString">withCString</a> for creating C-style strings from [Char]s. The function signature is:</p>
<pre>withCString :: String -> (CString -> IO a) -> IO a</pre>
<p>However, I don't have an IO monad. In this case, let's say I have a "ReaderT Parser IO". No problem, we'll just apply a liftIO... somewhere... and...</p>
<p>This is a little tricky in fact. What we <b>want</b> here is a function of type signature:</p>
<pre>MonadIO m => String -> (CString -> m a) -> m a</pre>
<p>Unfortunately, there's no way to create it automatically. Now, for the withCString function, we could go ahead and implement things ourselves. But what about <a href="http://www.haskell.org/ghc/docs/latest/html/libraries/base/Foreign-Marshal-Alloc.html#v%3AallocaBytes">allocaBytes</a>, which does some fancy magic under the hood?</p>
<h2>Dealing with ReaderT</h2>
<p>Before dealing with a generic solution, let's first play around with ReaderT and see what we come up with. Let's assume that we'll curry away the first argument to withCString, so we're left with a function with the type signature:</p>
<pre>(CString -> IO a) -> IO a</pre>
<p>We need to produce a function with the type signature:</p>
<pre>(CString -> ReaderT Parser IO a) -> ReaderT Parser IO a</pre>
<p>Turns out this function isn't too bad. We'll call it withReader (I wouldn't mind name suggestions):</p>
<pre>withReader :: ((CString -> IO a) -> IO a) -> (CString -> ReaderT Parser IO a) -> ReaderT Parser IO a
withReader orig f = ReaderT $ \parser -> do
    let f' a = (runReaderT $ f a) parser
    orig f'</pre>
<p>You can call this function like such:</p>
<pre>someOtherFunction :: CString -> ReaderT Parser IO SomeOutput
...
withReader (withCString "foobar") someOtherFunction :: ReaderT Parser IO SomeOutput</pre>
<h2>More general</h2>
<p>Well, that solved the problem. That is, until we want to be able to stack our monads. For example, let's say (for simplicity) that I wanted to have the following monad:</p>
<pre>ReaderT Parser (ReaderT Emitter IO)</pre>
<p>(In case you're wondering, this is code used for interleaving parsing and emitting of a YAML document. See <a href="http://www.snoyman.com/blog/entry/efficient-yaml-parsing/">my previous blog post</a> for more context.)</p>
<p>In any event, we <i>could</i> write a withReaderReader function, but instead I smell a great opportunity for a typeclass:</p>
<pre>class With m where
    with :: ((a -> IO b) -> IO b) -> (a -> m b) -> m b</pre>
<p>I currently only have two instances of this class, but I'm sure other monads could be easily added:</p>
<pre>instance With IO where -- obvious instance
    with = id
instance With m => With (ReaderT r m) where
    with orig f = ReaderT $ \r -> do
        let f' a = (runReaderT $ f a) r
        with orig f'</pre>
<h2>Improvements</h2>
<p>The number one improvement I need is a better name for this beast. After that, I'd like to add some more methods to handle even more useful functions, such as finally. I know I've been caught in the past wanting to ensure resource release when dealing with a monad stack.</p>
<p>This code (with slightly different names) is part of <a href="http://github.com/snoyberg/yaml/blob/master/Text/Libyaml.hs">my yaml github repo</a>. If there's demand, I'll split it into its own package.</p>
